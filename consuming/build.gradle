plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'uk.co.boothen.gradle.wsimport' version '0.25'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot about consuming a SOAP service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation ('org.springframework.boot:spring-boot-starter-webservices') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-webmvc'
    }
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

wsimport {
    wsdlSourceRoot = 'http://localhost:8080/services/'

    wsdl ("countries.wsdl") {
        packageName("com.example.consumingwebservice.wsdl")
        xjcarg("-XautoNameResolution")
    }
}

tasks.named('assemble') {
    // This forces the producer to run its assemble task first
    dependsOn ':producing:assemble'
}
